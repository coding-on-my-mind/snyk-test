plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "org.sonarqube" version "2.8"
    id "jacoco"
    id "org.flywaydb.flyway" version "6.4.1"
    id "com.github.jk1.dependency-license-report" version "1.13"
}

repositories {
    mavenCentral()
}

ext {
    ruleSets = resources.text.fromFile("src/main/resources/config/pmd/pmd-ruleset.xml")
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "pmd"
    apply plugin: "checkstyle"
    apply plugin: "jacoco"

    ext {
        springCloudVersion = "Hoxton.SR1"
        springBootVersion = "2.3.4.RELEASE"
        cfLibVersion = "4.3.0.RELEASE"
        set("project.pmd.ruleSetFiles", "config/pmd/pmd-ruleset.xml")
    }

    group = "com.coding-on-my-mind.stackit"
    version = "0.0.1-SNAPSHOT"

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    pmd {
        ignoreFailures = false
        reportsDir = file("$project.buildDir/reports/pmd")
        sourceSets {}
        ruleSets = [] // Remove built-in rule sets
        ruleSetConfig = project(":configuration").resources.text.fromFile("src/main/resources/config/pmd/pmd-ruleset.xml")
        toolVersion = "6.19.0"
        incrementalAnalysis = true
    }

    checkstyle {
        configFile = project(":configuration").file("./src/main/resources/config/checkstyle/checkstyle_checks.xml")
        configDirectory = project(":configuration").file("./src/main/resources/config/checkstyle")
        ignoreFailures = false
        toolVersion = "8.26"
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
        }
    }

}
